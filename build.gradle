import com.badlogic.gdx.tools.texturepacker.TexturePacker

buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		google()
		gradlePluginPortal()
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
	}
	dependencies {
		classpath "com.mobidevelop.robovm:robovm-gradle-plugin:$robovmVersion"
		classpath "com.android.tools.build:gradle:4.2.1"
		classpath "org.wisepersist:gwt-gradle-plugin:$gwtPluginVersion"
		classpath 'com.badlogicgames.gdx:gdx-tools:1.10.0'
	}
}

allprojects {
	apply plugin: 'eclipse'
	apply plugin: 'idea'
}

configure(subprojects - project(':android')) {
	apply plugin: 'java-library'
	sourceCompatibility = 8.0
	compileJava {
		options.incremental = true
	}

	task packTextures {
		def inputDir = "$rootDir/core/rawAssets/sprites"
		def outputDir = "$rootDir/assets/images/packaged"
		inputs.dir inputDir
		outputs.dir outputDir
		doLast {
			delete(outputDir)
			TexturePacker.process(inputDir, outputDir, "gigagal.pack")
		}
	}

	task copyLevels(type: Copy){
		from "$rootDir/GigaGalLevels/scenes"
		into "$rootDir/assets/levels"
		exclude "MainScene.dt"
	}

	task copyParicles(type: Copy){
		from "$rootDir/core/rawAssets/particles"
		exclude "*.png"
		into "$rootDir/assets/particles"
	}

	task copyBackgroundSprites(type: Copy){
		from "$rootDir/core/rawAssets/backgroundSprites"
		into "$rootDir/assets/images/backgroundSprites"
	}

	project.tasks.compileJava.dependsOn packTextures
	project.tasks.compileJava.dependsOn copyLevels
	project.tasks.compileJava.dependsOn copyParicles
	project.tasks.compileJava.dependsOn copyBackgroundSprites
}

subprojects {
	version = '1.0.0'
	ext.appName = 'GigaGal'
	repositories {
		mavenLocal()
		mavenCentral()
		gradlePluginPortal()
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
		maven { url 'https://jitpack.io' }
	}
}

eclipse.project.name = 'GigaGal' + '-parent'
